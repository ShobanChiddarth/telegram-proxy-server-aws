locals {
    base_init = <<-EOF
    #!/bin/bash
    apt-get update
    apt-get upgrade -y
EOF

    nat_instance_init = <<-EOF
    set -eux

    export DEBIAN_FRONTEND=noninteractive

    echo iptables-persistent iptables-persistent/autosave_v4 boolean true | debconf-set-selections
    echo iptables-persistent iptables-persistent/autosave_v6 boolean true | debconf-set-selections

    apt-get install -y iptables-persistent

    sysctl -w net.ipv4.ip_forward=1
    echo "net.ipv4.ip_forward=1" > /etc/sysctl.d/99-nat.conf

    PRIMARY_IFACE=$(ip route | awk '/default/ {print $5; exit}')

    iptables -t nat -A POSTROUTING -o "$PRIMARY_IFACE" -j MASQUERADE
    iptables -A FORWARD -i "$PRIMARY_IFACE" -m state --state RELATED,ESTABLISHED -j ACCEPT
    iptables -A FORWARD -o "$PRIMARY_IFACE" -j ACCEPT

    netfilter-persistent save
EOF
}

resource "aws_instance" "bastion" {
    ami = data.aws_ami.ubuntu.id
    instance_type = "t3.micro"
    subnet_id = aws_subnet.ManagementSubnet.id
    # associate_public_ip_address = true
    user_data = local.base_init
    vpc_security_group_ids = [ aws_security_group.bastion_sg.id ]
    key_name = aws_key_pair.bastion_server_key_pair.key_name

    tags = {
      "Name" = "Bastion"
    }
}

resource "aws_eip" "bastion_eip" {
    # elastic IP for bastion because new IP wont be trusted in SSH client
    instance = aws_instance.bastion.id
    domain = "vpc"

     tags = {
       "Name" = "bastion_instance_eip"
     }

     depends_on = [ aws_internet_gateway.NatInstanceDemoIGW ]
}


resource "aws_instance" "nat_instance" {
    ami = data.aws_ami.ubuntu.id
    instance_type = "t3.micro"
    subnet_id = aws_subnet.ManagementSubnet.id
    # associate_public_ip_address = true
    user_data = join("\n", [local.base_init, local.nat_instance_init])
    vpc_security_group_ids = [ aws_security_group.nat_instance_sg.id ]
    key_name = aws_key_pair.management_key_pair.key_name
    source_dest_check = false

    tags = {
      "Name" = "NAT_instance"
    }
}

resource "aws_eip" "nat_instance_eip" {
     instance = aws_instance.nat_instance.id
     domain = "vpc"

     tags = {
       "Name" = "nat_instance_eip"
     }

     depends_on = [ aws_internet_gateway.TProxyIGW ]
}
